

function UI_CreateAutoLayout( parent, hexpand )
{
	AutoLayout = parent.frame.createControl( "autolayout" );
	AutoLayout._childAffectsLayout = true;
	AutoLayout.data = { hexpand = hexpand };
	
	AutoLayout_data = AutoLayout.data;
	function AutoLayout_data.recalc( AutoLayout )
	{
		maxwidth = AutoLayout.rx1 - AutoLayout.rx0;
		x = 0;
		y = 0;
		mostwidth_total = 0;
		mostheight_line = 0;
		
		children = AutoLayout.children();
		foreach( ch : children )
		{
			ch._updatingLayout = true;
			chwidth = max( ch.rx1 - ch.rx0, ch.minWidth );
			chheight = max( ch.height, ch.minHeight );
			if( x > 0 && x + chwidth - toint(AutoLayout._roundedCoords || ch._roundedCoords) > maxwidth ) /* compensate for rounding errors with offset=1 */
			{
				y += mostheight_line;
				mostwidth_total = max( mostwidth_total, x );
				x = 0;
				mostheight_line = 0;
			}
			
			ch.x = x;
			ch.y = y;
			
			x += chwidth;
			mostheight_line = max( mostheight_line, chheight );
		}
		foreach( ch : children )
		{
			ch._updatingLayout = false;
			ch.updateLayout();
		}
		y += mostheight_line;
		mostwidth_total = max( mostwidth_total, x );
		
	//	if( AutoLayout.q1x == 0 ) AutoLayout.width = mostwidth_total;
		if( AutoLayout.q1y == 0 ) AutoLayout.height = y;
	}
	
	orig_callback = AutoLayout.callback;
	function AutoLayout.callback( event ) use( orig_callback )
	{
		data = this.data;
		if( event.type == EV_Layout )
		{
			this!orig_callback( event );
			data.recalc( this );
		}
		return this!orig_callback( event );
	}
	
	parent.addChild( AutoLayout );
	
	return AutoLayout;
}

UI_Controls.autolayout_params = [];
function UI_Controls.autolayout( parent, params )
{
	ctrl = UI_CreateAutoLayout( parent );
	UI_ConfigControl( ctrl, params );
}


global UI_Scrollable = {};

function UI_Scrollable._doScrolling( factor )
{
	factor ||= 1;
	data = this.data;
	if( data.cursor_scroll )
	{
		curx = data.cursor_x;
		cury = data.cursor_y;
		if( data.scrV.visible && curx >= this.cx0 && curx < this.cx1 )
		{
			scrfac = 0;
			if( cury >= this.cy0 && cury < this.py0 ) scrfac--;
			if( cury >= this.py1 && cury < this.cy1 ) scrfac++;
			if( scrfac )
				data.scrV.moveRel( data.scrV.data.mousemove_step * scrfac * factor );
		}
		if( data.scrH.visible && cury >= this.cy0 && cury < this.cy1 )
		{
			scrfac = 0;
			if( curx >= this.cx0 && curx < this.px0 ) scrfac--;
			if( curx >= this.px1 && curx < this.cx1 ) scrfac++;
			if( scrfac )
				data.scrH.moveRel( data.scrH.data.mousemove_step * scrfac * factor );
		}
		
		me = this;
		this.frame.setTimeout( 1/60, function() use(me){ me._doScrolling(1/60); } );
	}
}

function UI_Scrollable.enableCursorScroll( enable ){ this.data.cursor_scroll = enable; }

function UI_CreateScrollable( parent, vscroll, hscroll )
{
	vscroll = if( vscroll === null, "auto", vscroll );
	hscroll = if( hscroll === null, "auto", hscroll );
	
	Scrollable = parent.frame.createControl( "scrollable" );
	Scrollable._childAffectsLayout = true;
	Scrollable._interface = UI_Scrollable;
	Scrollable.data =
	{
		x0 = 0, x1 = 0, y0 = 0, y1 = 0,
		hscroll = hscroll,
		vscroll = vscroll,
		
		cursor_scroll = false,
		cursor_x = null,
		cursor_y = null,
	};
	
	Scrollable.data.scrV = scrV = UI_CreateVScrollbar( Scrollable )[]{ nonclient = true };
	Scrollable.data.scrH = scrH = UI_CreateHScrollbar( Scrollable )[]{ nonclient = true };
	scrV.addClass( "scroll-vertical" );
	scrH.addClass( "scroll-horizontal" );
	
	Scrollable_data = Scrollable.data;
	function Scrollable_data.recalc( Scrollable ) use( scrV, scrH )
	{
		chlist = Scrollable.children();
		x0 = x1 = y0 = y1 = 0;
		foreach( ch : chlist )
		{
			p0x = ch.rx0 - Scrollable.cx0;
			p0y = ch.ry0 - Scrollable.cy0;
			p1x = ch.rx1 - Scrollable.cx0;
			p1y = ch.ry1 - Scrollable.cy0;
			
			x0 = min( x0, min( p0x, p1x ) );
			x1 = max( x1, max( p0x, p1x ) );
			y0 = min( y0, min( p0y, p1y ) );
			y1 = max( y1, max( p0y, p1y ) );
		}
		
		this.x0 = x0; this.x1 = x1;
		this.y0 = y0; this.y1 = y1;
		
		iw = x1 - x0;
		ih = y1 - y0;
		
		showV = if( this.vscroll === "auto", ih > Scrollable.realHeight - scrV.realWidth * scrH.visible, this.vscroll );
		showH = if( this.hscroll === "auto", iw > Scrollable.realWidth - scrH.realHeight * scrV.visible, this.hscroll );
		
		scrV.visible = showV;
		scrH.visible = showH;
		scrV.data.space = ih;
		scrH.data.space = iw;
		fs = Scrollable.filteredStyle;
		Scrollable.nonClientRight = fs.nonClientRight + if( showV, scrV.realWidth, 0 );
		Scrollable.nonClientBottom = fs.nonClientBottom + if( showH, scrH.realHeight, 0 );
	}
	
	scrV.bindEvent( "change", function() use( Scrollable ){ Scrollable.scroll_y = -this.data.position; } );
	scrH.bindEvent( "change", function() use( Scrollable ){ Scrollable.scroll_x = -this.data.position; } );
	
	ocb = Scrollable.callback;
	function Scrollable.callback( event ) use( ocb, scrV, scrH )
	{
		data = this.data;
		if( event.type == EV_Layout )
		{
			this!ocb( event );
			data.recalc( this );
		}
		else if( event.type == EV_MouseWheel )
		{
			if( scrV.visible )
				scrV.callback( event );
			if( scrH.visible )
			{
				event.y = event.x;
				scrH.callback( event );
			}
			return 0; // stop bubbling
		}
		else if( event.type == EV_MouseMove )
		{
			data.cursor_x = event.x;
			data.cursor_y = event.y;
			this._doScrolling();
		}
		return this!ocb( event );
	}
	
	Scrollable.updateTheme();
	parent.addChild( Scrollable );
	return Scrollable;
}

UI_Controls.scrollable_params = ["vscroll","hscroll"];
function UI_Controls.scrollable( parent, params )
{
	ctrl = UI_CreateScrollable( parent, @params.vscroll, @params.hscroll );
	UI_ConfigControl( ctrl, params );
}


global UI_SplitPane = {};
function UI_SplitPane.getPaneA(){ return this.data.PaneA; }
function UI_SplitPane.getPaneB(){ return this.data.PaneB; }
function UI_SplitPane.getSplit(){ return this.data.Split; }
function UI_SplitPane.getSplitFactor()
{
	data = this.data;
	return if( data.vertical, data.Split.q0x, data.Split.q0y );
}
function UI_SplitPane.setSplitFactor( split )
{
	data = this.data;
	if( !data.vertical )
	{
		data.PaneA.q1y = split;
		data.PaneB.q0y = split;
		data.Split[]{ q0y = split, q1y = split };
	}
	else
	{
		data.PaneA.q1x = split;
		data.PaneB.q0x = split;
		data.Split[]{ q0x = split, q1x = split };
	}
}

function UI_CreateSplitPane( parent, vertical, defsplit )
{
	if( defsplit === null )
		defsplit = 0.5;
	SplitPane = parent.frame.createControl( "splitpane" );
	SplitPane.class = if( vertical, "vertical", "horizontal" );
	SplitPane.data = { vertical = vertical };
	SplitPane._interface = UI_SplitPane;
	
	SplitPane.data.PaneA = PaneA = parent.frame.createControl( "pane" )[]{ class = "pane-a" };
	SplitPane.data.PaneB = PaneB = parent.frame.createControl( "pane" )[]{ class = "pane-b" };
	SplitPane.data.Split = Split = parent.frame.createControl( "split" );
	
	Split.data = { dragging = false, dragoff_x = null, dragoff_y = null };
	ocb = Split.callback;
	function Split.callback( event ) use( ocb )
	{
		data = this.data;
		if( event.type == EV_ButtonDown )
		{
			if( event.button == Mouse_ButtonL )
			{
				data.dragoff_x = ( this.rx0 + this.rx1 ) / 2 - event.x;
				data.dragoff_y = ( this.ry0 + this.ry1 ) / 2 - event.y;
				data.dragging = true;
			}
		}
		else if( event.type == EV_ButtonUp )
		{
			if( event.button == Mouse_ButtonL )
			{
				data.dragging = false;
			}
		}
		else if( event.type == EV_MouseMove )
		{
			if( data.dragging )
			{
				if( this.parent.data.vertical )
				{
					rcx = event.x + data.dragoff_x;
					pqx = this.q0x;
					pw = max( this.parent.rx1 - this.parent.rx0, 1 );
					cqx = ( rcx - this.parent.rx0 ) / pw;
					if( cqx < -this.x / pw )
						cqx = -this.x / pw;
					if( cqx > 1 - ( this.x + this.width ) / pw )
						cqx = 1 - ( this.x + this.width ) / pw;
					if( pqx != cqx )
						this.parent.setSplitFactor( cqx );
				}
				else
				{
					rcy = event.y + data.dragoff_y;
					pqy = this.q0y;
					ph = max( this.parent.ry1 - this.parent.ry0, 1 );
					cqy = ( rcy - this.parent.ry0 ) / ph;
					if( cqy < -this.y / ph )
						cqy = -this.y / ph;
					if( cqy > 1 - ( this.y + this.height ) / ph )
						cqy = 1 - ( this.y + this.height ) / ph;
					if( pqy != cqy )
						this.parent.setSplitFactor( cqy );
				}
			}
		}
		return this!ocb( event );
	}
	
	SplitPane.addChild( PaneA );
	SplitPane.addChild( PaneB );
	SplitPane.addChild( Split );
	
	SplitPane.updateTheme();
	parent.addChild( SplitPane );
	
	SplitPane.setSplitFactor( defsplit );
	return SplitPane;
}
function UI_CreateHSplitPane( parent, defsplit ){ return UI_CreateSplitPane( parent, false, defsplit ); }
function UI_CreateVSplitPane( parent, defsplit ){ return UI_CreateSplitPane( parent, true, defsplit ); }


function UI_CreateMenuPanel( parent, items )
{
	MenuPanel = parent.frame.createControl( "menupanel" );
	
	items = clone( items );
	foreach( item : items )
	{
		item.text = @item.text;
	}
	// each item can have the following used parameters
	// - text (label text)
	// - func (trigger callback, "checked" is updated before this)
	// - menu (a submenu to display on click/after some time)
	// - disabled (whether entry is a text or a line separator)
	
	MenuPanel.data = mpdata =
	{
		item_height = 24,
		side_padding = 8,
		
		items = items,
		selected = false,
		
		parent_menu = null,
		menu_open = null,
		menu_open_for = null,
	};
	
	function mpdata.recalcItemWidths( MenuPanel )
	{
		foreach( item : this.items )
			item.width = MenuPanel._cachedFont.getTextLength( item.text ) + this.side_padding * 2;
	}
	
	function mpdata.recalcItemPositions( MenuPanel )
	{
		x = 0;
		y = 0;
		rw = MenuPanel.rx1 - MenuPanel.rx0;
		foreach( item : this.items )
		{
			if( x + item.width > rw )
			{
				x = 0;
				y += this.item_height;
			}
			item.x = x + MenuPanel.rx0;
			item.y = y + MenuPanel.ry0;
			x += item.width;
		}
		MenuPanel.nonClientTop = y + this.item_height;
	}
	
	function mpdata.removeOpen()
	{
		if( this.menu_open )
		{
			mo = this.menu_open;
			mo.parent.removeChild( mo );
			this.menu_open = null;
		}
		this.menu_open_for = null;
		this.selected = false;
	}
	function mpdata.removeRecursive( cc )
	{
		while( cc && cc.type == "menu" )
		{
			pp = cc.data.parent_menu;
			cc.parent.removeChild( cc );
			cc = pp;
		}
	}
	
	mpdata.recalcItemWidths( MenuPanel );
	mpdata.recalcItemPositions( MenuPanel );
	
	ocb = MenuPanel.callback;
	function MenuPanel.callback( event ) use( ocb )
	{
		data = this.data;
		if( event.type == EV_Layout )
		{
			res = this!ocb( event );
			data.recalcItemPositions( this );
			this.frame.handleMouseMove();
			return res;
		}
		else if( event.type == EV_MouseMove )
		{
			data.selected = false;
			foreach( i, item : data.items )
			{
				if( event.x >= item.x && event.y >= item.y &&
					event.x < item.x + item.width && event.y < item.y + data.item_height )
				{
					data.selected = i;
				}
			}
		}
		else if( event.type == EV_ButtonDown )
		{
			sel = data.selected; // store to avoid modification from the events called
			mof = data.menu_open_for;
			data.removeOpen();
			if( sel !== false )
			{
				item = data.items[ sel ];
				if( !@item.disabled )
				{
					if( isset( item, "menu" ) )
					{
						if( sel !== mof )
						{
							data.menu_open = UI_BuildMenu( this.frame.root, item.x, item.y + data.item_height, item.menu );
							data.menu_open.index = this.index + 1;
							data.menu_open.data.parent_menu = this;
							data.menu_open_for = sel;
							this.frame.handleMouseMove();
						}
					}
					else if( isset( item, "func" ) )
						item.func();
					
					data.selected = sel; // restore
				}
			}
		}
		return this!ocb( event );
	}
	
	MenuPanel.updateTheme();
	parent.addChild( MenuPanel );
	return MenuPanel;
}

UI_Controls.menupanel_params = ["items"];
function UI_Controls.menupanel( parent, params )
{
	ctrl = UI_CreateMenuPanel( parent, @params.items );
	UI_ConfigControl( ctrl, params );
}

