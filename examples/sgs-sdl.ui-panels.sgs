

function UI_CreateAutoLayout( parent, x, y, width, height )
{
	AutoLayout = parent.frame.createControl( "autolayout" );
	AutoLayout.x = x;
	AutoLayout.y = y;
	AutoLayout.width = width;
	AutoLayout.height = height;
//	AutoLayout.minWidth = 0;
//	AutoLayout.maxWidth = if( width !== null, width, UI_MaxValue );
//	AutoLayout.minHeight = 0;
//	AutoLayout.maxHeight = if( height !== null, height, UI_MaxValue );
	AutoLayout.data = {};
	
	AutoLayout_data = AutoLayout.data;
	function AutoLayout_data.recalc( AutoLayout )
	{
		maxwidth = AutoLayout.rx1 - AutoLayout.rx0;
		x = 0;
		y = 0;
		mostwidth_total = 0;
		mostheight_line = 0;
		
		children = AutoLayout.children();
		foreach( ch : children )
		{
			ch._updatingLayout = true;
			chwidth = max( ch.rx1 - ch.rx0, ch.minWidth );
			chheight = max( ch.height, ch.minHeight );
			if( x > 0 && x + chwidth - toint(AutoLayout._roundedCoords || ch._roundedCoords) > maxwidth ) /* compensate for rounding errors with offset=1 */
			{
				y += mostheight_line;
				mostwidth_total = max( mostwidth_total, x );
				x = 0;
				mostheight_line = 0;
			}
			
			ch.x = x;
			ch.y = y;
			
			x += chwidth;
			mostheight_line = max( mostheight_line, chheight );
		}
		foreach( ch : children )
		{
			ch._updatingLayout = false;
			ch.updateLayout();
		}
		y += mostheight_line;
		mostwidth_total = max( mostwidth_total, x );
		
	//	if( AutoLayout.q1x == 0 ) AutoLayout.width = mostwidth_total;
		if( AutoLayout.q1y == 0 ) AutoLayout.height = y;
	}
	
	orig_callback = AutoLayout.callback;
	function AutoLayout.callback( event ) use( orig_callback )
	{
		data = this.data;
		if( event.type == EV_Layout )
		{
			this!orig_callback( event );
			data.recalc( this );
		}
		return this!orig_callback( event );
	}
	
	parent.addChild( AutoLayout );
	
	return AutoLayout;
}

UI_Controls.autolayout_params = [];
function UI_Controls.autolayout( parent, params )
{
	ctrl = UI_CreateAutoLayout( parent, null, null, null, null );
	UI_ConfigControl( ctrl, params );
}


function UI_CreateScrollable( parent, x, y, width, height, vscroll, hscroll )
{
	vscroll = if( vscroll === null, "auto", vscroll );
	hscroll = if( hscroll === null, "auto", hscroll );
	
	Scrollable = parent.frame.createControl( "scrollable" );
	Scrollable.x = x;
	Scrollable.y = y;
	Scrollable.width = width;
	Scrollable.height = height;
	Scrollable.data =
	{
		padding = 1,
		scroll_size = 12,
		x0 = 0, x1 = 0, y0 = 0, y1 = 0,
		hscroll = hscroll,
		vscroll = vscroll,
	};
	
	pad = Scrollable.data.padding;
	ssize = Scrollable.data.scroll_size;
	
	scrV = UI_CreateVScroll( Scrollable )[]{ x = pad, y = -pad, width = ssize, height = pad * 2, nonclient = true, visible = false, q0x = 1, q1x = 1, q1y = 1 };
	scrH = UI_CreateHScroll( Scrollable )[]{ x = -pad, y = pad, width = pad * 2, height = ssize, nonclient = true, visible = false, q0y = 1, q1y = 1, q1x = 1 };
	
	Scrollable_data = Scrollable.data;
	function Scrollable_data.recalc( Scrollable ) use( scrV, scrH )
	{
		pad = this.padding;
		chlist = Scrollable.children();
		x0 = x1 = y0 = y1 = 0;
		foreach( ch : chlist )
		{
			p0x = ch.rx0 - Scrollable.rx0 - pad;
			p0y = ch.ry0 - Scrollable.ry0 - pad;
			p1x = ch.rx1 - Scrollable.rx0 - pad;
			p1y = ch.ry1 - Scrollable.ry0 - pad;
			
			x0 = min( x0, min( p0x, p1x ) );
			x1 = max( x1, max( p0x, p1x ) );
			y0 = min( y0, min( p0y, p1y ) );
			y1 = max( y1, max( p0y, p1y ) );
		}
		
		x1 += pad * 2;
		y1 += pad * 2;
		
		this.x0 = x0; this.x1 = x1;
		this.y0 = y0; this.y1 = y1;
		
		iw = x1 - x0;
		ih = y1 - y0;
		
		conth = Scrollable.ry1 - Scrollable.ry0;
		contw = Scrollable.rx1 - Scrollable.rx0;
		
		showV = if( this.vscroll === "auto", ih > conth - this.scroll_size * scrH.visible, this.vscroll );
		showH = if( this.hscroll === "auto", iw > contw - this.scroll_size * scrV.visible, this.hscroll );
		
		scrV.visible = showV;
		scrH.visible = showH;
		scrV.data.space = ih;
		scrH.data.space = iw;
		Scrollable.nonClientTop = pad;
		Scrollable.nonClientLeft = pad;
		Scrollable.nonClientRight = pad + if( showV, this.scroll_size, 0 );
		Scrollable.nonClientBottom = pad + if( showH, this.scroll_size, 0 );
	}
	
	scrV.bindEvent( "change", function() use( Scrollable ){ Scrollable.scroll_y = -this.data.position; } );
	scrH.bindEvent( "change", function() use( Scrollable ){ Scrollable.scroll_x = -this.data.position; } );
	
	orig_callback = Scrollable.callback;
	function Scrollable.callback( event ) use( orig_callback, scrV, scrH )
	{
		data = this.data;
		if( event.type == EV_Layout )
		{
			data.recalc( this );
		}
		else if( event.type == EV_MouseWheel )
		{
			if( scrV.visible )
				scrV.callback( event );
			if( scrH.visible )
			{
				event.y = event.x;
				scrH.callback( event );
			}
			return 0; // stop bubbling
		}
		return this!orig_callback( event );
	}
	
	Scrollable.updateTheme();
	parent.addChild( Scrollable );
	return Scrollable;
}

function UI_CreateMenuPanel( parent, items )
{
	MenuPanel = parent.frame.createControl( "menupanel" );
	
	items = clone( items );
	foreach( item : items )
	{
		item.text = @item.text;
	}
	// each item can have the following used parameters
	// - text (label text)
	// - func (trigger callback, "checked" is updated before this)
	// - menu (a submenu to display on click/after some time)
	// - disabled (whether entry is a text or a line separator)
	
	MenuPanel.data = mpdata =
	{
		item_height = 24,
		side_padding = 8,
		
		items = items,
		selected = false,
		
		parent_menu = null,
		menu_open = null,
		menu_open_for = null,
	};
	
	function mpdata.recalcItemWidths( MenuPanel )
	{
		foreach( item : this.items )
			item.width = MenuPanel._cachedFont.getTextLength( item.text ) + this.side_padding * 2;
	}
	
	function mpdata.recalcItemPositions( MenuPanel )
	{
		x = 0;
		y = 0;
		rw = MenuPanel.rx1 - MenuPanel.rx0;
		foreach( item : this.items )
		{
			if( x + item.width > rw )
			{
				x = 0;
				y += this.item_height;
			}
			item.x = x + MenuPanel.rx0;
			item.y = y + MenuPanel.ry0;
			x += item.width;
		}
		MenuPanel.nonClientTop = y + this.item_height;
	}
	
	function mpdata.removeOpen()
	{
		if( this.menu_open )
		{
			mo = this.menu_open;
			mo.parent.removeChild( mo );
			this.menu_open = null;
		}
		this.menu_open_for = null;
		this.selected = false;
	}
	function mpdata.removeRecursive( cc )
	{
		while( cc && cc.type == "menu" )
		{
			pp = cc.data.parent_menu;
			cc.parent.removeChild( cc );
			cc = pp;
		}
	}
	
	mpdata.recalcItemWidths( MenuPanel );
	mpdata.recalcItemPositions( MenuPanel );
	
	ocb = MenuPanel.callback;
	function MenuPanel.callback( event ) use( ocb )
	{
		data = this.data;
		if( event.type == EV_Layout )
		{
			data.recalcItemPositions( this );
			this.frame.handleMouseMove();
		}
		else if( event.type == EV_MouseMove )
		{
			data.selected = false;
			foreach( i, item : data.items )
			{
				if( event.x >= item.x && event.y >= item.y &&
					event.x < item.x + item.width && event.y < item.y + data.item_height )
				{
					data.selected = i;
				}
			}
		}
		else if( event.type == EV_ButtonDown )
		{
			sel = data.selected; // store to avoid modification from the events called
			mof = data.menu_open_for;
			data.removeOpen();
			if( sel !== false )
			{
				item = data.items[ sel ];
				if( !@item.disabled )
				{
					if( isset( item, "menu" ) )
					{
						if( sel !== mof )
						{
							data.menu_open = UI_BuildMenu( this.frame.root, item.x, item.y + data.item_height, item.menu );
							data.menu_open.index = this.index + 1;
							data.menu_open.data.parent_menu = this;
							data.menu_open_for = sel;
							this.frame.handleMouseMove();
						}
					}
					else if( isset( item, "func" ) )
						item.func();
					
					data.selected = sel; // restore
				}
			}
		}
		return this!ocb( event );
	}
	
	MenuPanel.updateTheme();
	parent.addChild( MenuPanel );
	return MenuPanel;
}

